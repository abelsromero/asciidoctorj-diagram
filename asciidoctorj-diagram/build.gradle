dependencies {

  testCompile "org.jsoup:jsoup:$jsoupVersion"
  testCompile "org.asciidoctor:asciidoctorj:$asciidoctorJVersion"

  gems("rubygems:asciidoctor-diagram:$asciidoctorDiagramGemVersion") {
    // Exclude gems provided by AsciidoctorJ core
    exclude module: 'asciidoctor'
    exclude module: 'thread_safe'
  }
}

def gemFiles = fileTree("${project.buildDir}/.gems") {
  include 'specifications/*.gemspec'
  include 'gems/*/lib/**'
  include "gems/asciidoctor-diagram-${asciidoctorDiagramGemVersion}/data/**"
}

jrubyPrepare {
  doLast {
    println "Copy from $gemFiles into $preparedGems"
    copy { // bundles the gems inside this artifact
      from gemFiles
      into preparedGems
    }
  }
}

ext.publicationName = "mavenAsciidoctorJDiagram"

apply from: rootProject.file('gradle/publish.gradle')
apply from: rootProject.file('gradle/signing.gradle')